#!/usr/bin/perl -CS

use strict;
use warnings;
use 5.010;

use Encode 'encode_utf8';
use open ':std', ':encoding(UTF-8)';

use Text::vFile::asData;
use LWP::Simple;
use JSON;

my $conf_file = shift || 'config.json';

open my $conf_fh, '<', $conf_file or die "$conf_file: $!";
my $json = do { local $/; <$conf_fh> };
my $config = decode_json($json);

my $combined = {
  type => 'VCALENDAR',
  properties => {
    'X-WR-CALNAME' => [ { value => $config->{title} } ],
  },
  objects => [],
};

for (@{$config->{feeds}}) {
  my $ics = get($_) or die "Can't get $_";
  $ics = encode_utf8($ics);
  open my $fh, '<', \$ics or die $!;
  my $data = Text::vFile::asData->new->parse( $fh );

  push @{ $combined->{objects} }, @{ $data->{objects}[0]{objects} };
}

say "$_\r" for Text::vFile::asData->generate_lines($combined);
